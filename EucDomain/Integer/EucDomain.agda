-- Example, â„¤ is an Euclidean Domain. Here all the properties needed
-- are already proved in Data.Integer.Properties. However, we will
-- prove there is another divmod pair such that the rank estimation
-- is more precise, see EucDomain2.agda.

{-# OPTIONS --without-K --safe #-}

module Integer.EucDomain where

  -- imports from stdlib.
  open import Relation.Nullary using (Â¬_)
  open import Relation.Binary.PropositionalEquality using (refl ; _â‰¡_)

  open import Data.Nat using (_<_)
  open import Data.Integer
    using (_+_ ; _*_ ; -_ ; â„¤ ; 0â„¤ ; 1â„¤ ; âˆ£_âˆ£ ; +[1+_] ; -[1+_] ; +_)
  open import Data.Integer.DivMod
    renaming (_div_ to _divâ„¤_; _mod_ to _modâ„¤_ ; n%d<d to euc-rankâ„¤; aâ‰¡a%n+[a/n]*n to euc-eqâ„¤)
  open import Data.Integer.Properties
    using (+-*-isCommutativeRing ; *-cancelË¡-â‰¡)

  open import Algebra.Definitions (_â‰¡_ {A = â„¤}) using (AlmostLeftCancellative)

  -- imports from local.
  import EuclideanDomain
  open EuclideanDomain.Structures (_â‰¡_ {A = â„¤})
  open EuclideanDomain.Bundles


  -- You will notice the recurring pattern that I have to split on
  -- the divisor d, and use Â¬ d â‰¡ 0â„¤ to exclude the zero divisor
  -- case. And addtionally, I have to repeat similary codes
  -- twice. This is the price of the translation from Â¬ a â‰¡ 0â„¤ to
  -- NonZero a. I don't have a good way to avoid this. This become
  -- even more awkward when dealing with Gaussian integers since we
  -- have more constructor there (meaning more cases and more
  -- repeatitions).

  -- On the other hand, a translation from NonZero predicate to
  -- non-equaity Â¬ d â‰¡ 0â„¤ is quite easy. We will use this to define
  -- div and mod with instance predicate argument for ð”¾.

  -- I could use NonZero predicate in the definition of Euclidean
  -- Domain, but that will make the definition more complicated. Also
  -- the file "Algebra.Definition" using the same method as here to
  -- exclude zero, for example the definition of
  -- "AlmostLeftCancellative", so we also comply this convention.

  -- div with irrelevant instance argument replaced by non-equality
  -- argument.
  div : âˆ€ (n d : â„¤) -> Â¬ d â‰¡ 0â„¤ -> â„¤
  div n (+ 0) n0 with n0 refl
  ... | ()
  div n d@(+[1+ nâ‚ ]) n0 = n divâ„¤ d 
  div n d@(-[1+_] nâ‚) n0 = n divâ„¤ d

  -- mod with irrelevant instance argument replaced by non-equality
  -- argument. 
  mod : âˆ€ (n d : â„¤) -> Â¬ d â‰¡ 0â„¤ -> â„¤
  mod n (+ 0) n0 with n0 refl
  ... | ()
  mod n d@(+[1+ nâ‚ ]) n0 = + (n modâ„¤ d)
  mod n d@(-[1+_] nâ‚) n0 = + (n modâ„¤ d)

  -- Divident = reminder + quotient * divisor.
  euc-eq : âˆ€ (n d : â„¤) (n0 : Â¬ d â‰¡ 0â„¤) ->
               let r = mod n d n0 in let q = div n d n0 in 
               n â‰¡ r + q * d 
  euc-eq n (+ 0) n0 with n0 refl
  ... | ()
  euc-eq n d@(+[1+ nâ‚ ]) n0 = euc-eqâ„¤ n d 
  euc-eq n d@(-[1+_] nâ‚) n0 = euc-eqâ„¤ n d 


  -- The rank of the reminder is strictly smaller than the rank of the
  -- divisor.
  euc-rank : âˆ€ (n d : â„¤) (n0 : Â¬ d â‰¡ 0â„¤) ->
               let r = mod n d n0 in let q = div n d n0 in 
               âˆ£ r âˆ£ < âˆ£ d âˆ£ 
  euc-rank n (+ 0) n0 with n0 refl
  ... | ()
  euc-rank n d@(+[1+ nâ‚ ]) n0 = euc-rankâ„¤ n d
  euc-rank n d@(-[1+_] nâ‚) n0 = euc-rankâ„¤ n d


  -- Multiplication is left cancellative. 
  *-alc-â„¤ : AlmostLeftCancellative 0â„¤ _*_
  *-alc-â„¤ {+ 0} j k n0 with n0 refl
  ... | ()
  *-alc-â„¤ {i@(+[1+ n ])} j k n0 = *-cancelË¡-â‰¡ i j k
  *-alc-â„¤ { i@(-[1+ n ])} j k n0 = *-cancelË¡-â‰¡ i j k

  -- â„¤ is an Euclidean Domain. 
  +-*-isEuclideanDomain : IsEuclideanDomain _+_ _*_ -_ 0â„¤ 1â„¤
  +-*-isEuclideanDomain = record
                            { isCommutativeRing = +-*-isCommutativeRing 
                            ; *-alc = *-alc-â„¤
                            ; div = div 
                            ; mod = mod 
                            ; rank = âˆ£_âˆ£
                            ; euc-eq = euc-eq 
                            ; euc-rank = euc-rank 
                            }

  -- Bundle.
  +-*-euclideanDomain : EuclideanDomainBundle _ _
  +-*-euclideanDomain = record
    { isEuclideanDomain = +-*-isEuclideanDomain
    }

